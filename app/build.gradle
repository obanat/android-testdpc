apply plugin: 'com.android.application'

ext {
    /* Version code for *next* release, bump *after* a release is created. */
    // 1 or more digits
    versionMajor = 9
    // exactly 1 digit
    versionMinor = 0
    // exactly 2 digits
    versionBuild = 04
}

android {
    compileSdkVersion 34
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.afwsamples.testdpc"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode versionMajor * 1000 + versionMinor * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionBuild}"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "packageName"
    productFlavors {
        normal {
            dimension 'packageName'
        }
        replica {
            dimension 'packageName'
            applicationIdSuffix ".replica"
        }
    }

    lintOptions {
        check 'NewApi'
        abortOnError false
        xmlReport false
        textReport true
        textOutput "stdout"
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
        maven {
            url 'https://maven.google.com'
        }
    }

    // Enable lint checking in all build variants.
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def lintTask = tasks["lint${variant.name.capitalize()}"]
            output.assemble.dependsOn lintTask
        }
    }
}

dependencies {
    implementation(name: 'setupcompat', ext: 'aar')
    implementation(name: 'setupdesign', ext: 'aar')
    implementation(name: 'strings', ext: 'aar')

    implementation "androidx.annotation:annotation:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "androidx.core:core:1.6.0"
    implementation "androidx.enterprise:enterprise-feedback:1.1.0"
    implementation "androidx.legacy:legacy-support-core-ui:1.0.0"
    implementation "androidx.legacy:legacy-support-v13:1.0.0"
    implementation "androidx.lifecycle:lifecycle-common:2.4.1"
    implementation "androidx.lifecycle:lifecycle-process:2.4.1"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0"
    implementation "androidx.preference:preference:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.2.0"
    implementation "androidx.collection:collection:1.2.0"
    implementation "com.google.android.material:material:1.5.0"
    implementation "com.google.guava:guava:31.1-android"
    implementation "org.bouncycastle:bcpkix-jdk15on:1.70"
    implementation "org.bouncycastle:bcprov-jdk15on:1.70"
}
